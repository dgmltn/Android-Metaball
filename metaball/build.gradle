apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    resourcePrefix 'mv_'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 23
        versionCode rootProject.ext.libraryVersionCode
        versionName rootProject.ext.libraryVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:24.0.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.android.support:design:24.0.0'
}

// Publish to Bintray

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    excludes = ['**/*.kt']
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId rootProject.ext.bintrayGroupId
            artifactId rootProject.ext.bintrayArtifactId
            version rootProject.ext.libraryVersion

            artifact(javadocJar)
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/metaball-release.aar")
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven'
        name = rootProject.ext.bintrayArtifactId
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/dgmltn/Android-Metaball.git'
        version {
            name = rootProject.ext.libraryVersion
            vcsTag = rootProject.ext.libraryVersion
        }
    }
}
repositories {
    mavenCentral()
}
