apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    resourcePrefix 'mv_'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 24
        versionCode rootProject.ext.libraryVersionCode
        versionName rootProject.ext.libraryVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:24.2.1'
}

// Publish to Bintray

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId rootProject.ext.bintrayGroupId
            artifactId rootProject.ext.bintrayArtifactId
            version rootProject.ext.libraryVersion

            artifact(javadocJar)
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/metaball-release.aar")
        }
    }
}

uploadArchives {
    repositories {
        apply plugin: 'maven'
        mavenDeployer {
            snapshotRepository(url: 'file://' + new File(System.getProperty('user.home'), 'LocalRepository/snapshots').absolutePath) { }

            pom.version = rootProject.ext.libraryVersion
            pom.artifactId = rootProject.ext.bintrayArtifactId
            pom.groupId = rootProject.ext.bintrayGroupId
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayApiKey
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven'
        name = rootProject.ext.bintrayArtifactId
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/dgmltn/Android-Metaball.git'
        version {
            name = rootProject.ext.libraryVersion
            vcsTag = rootProject.ext.libraryVersion
        }
    }
}
